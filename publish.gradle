apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }


            def release = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshot = 'https://oss.sonatype.org/content/repositories/snapshots/'
            repository(url: project.version.contains('SNAPSHOT') ? snapshot : release) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Gradle wsdl2java plugin'
                description 'The Gradle wsdl2java plugin makes it easy to generate Java source from WSDL'
                url 'http://github.com/janisz/gradle-wsdl2java'
                inceptionYear '2014'

                scm {
                    developerConnection 'git@github.com:janisz/gradle-wsdl2java.git'
                    connection 'git://github.com/janisz/gradle-wsdl2java.git'
                    url 'https://github.com/janisz/gradle-wsdl2java.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'janisz'
                        name 'Tomasz Janiszewski'
                        email 'janiszt@gmail.com'
                        roles {
                            role 'Developer'
                        }
                        timezone '+1'
                    }
                }
            }
        }
    }
}